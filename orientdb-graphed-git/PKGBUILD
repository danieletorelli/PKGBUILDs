# Maintainer:  Michele Damiano Torelli <me_AT_mdtorelli_DOT_it>

pkgname=orientdb-graphed-git
pkgver=20130131
pkgrel=1
pkgdesc="A NoSQL Graph-Document DBMS - Graph Edition (with TinkerPop Blueprints, Gremlin and Pipes) - Development version"
conflicts=('orientdb' 'orientdb-graphed' 'orientdb-git')
arch=('any')
license=('Apache')
url="http://www.orientdb.org"
depends=('java-runtime-headless')
makedepends=('git' 'apache-ant')
install=$pkgname.install
source=('orientdb.service')
md5sums=('64c40019196576036766e7b92729a741')

_gitroot="https://github.com/nuvolabase/orientdb"
_gitname="orientdb"

build() {
  msg "Connecting to GIT server...."
  
  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi
  
  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/$_gitname-build"
  git clone "${srcdir}/$_gitname" "${srcdir}/$_gitname-build"
  cd "${srcdir}/$_gitname-build"

  . /etc/profile.d/apache-ant.sh
  ant -DreleaseHome=. installg
}

package() {
  cd "$_gitname-build"

  _gitversion="$(cat build.xml | grep 'property name="version"' | head -1 | sed -r 's|<property name="version" value="||; s|"/>||; s|^[ \t]*||; s|[ \t]*$||; s|[ \r]*$||')"
  msg "Git version: $_gitversion"

  cd "$_gitname-graphed-$_gitversion"

  install -dm755 "${pkgdir}"/opt/orientdb
  install -dm700 "${pkgdir}"/opt/orientdb/config
  install -dm700 "${pkgdir}"/opt/orientdb/databases
  install -dm755 "${pkgdir}"/opt/orientdb/bin
  install -dm700 "${pkgdir}"/opt/orientdb/www
  install -dm755 "${pkgdir}"/opt/orientdb/lib
 
  install -d "${pkgdir}"/usr/bin
  install -d "${pkgdir}"/var/log/orientdb
  install -d "${pkgdir}"/usr/lib/systemd/system
 
  sed -i 's|\.\./log|/opt/orientdb/log|' config/orientdb-server-log.properties
  sed -i 's|YOUR_ORIENTDB_INSTALLATION_PATH|/opt/orientdb|' bin/orientdb.sh
  sed -i 's|USER_YOU_WANT_ORIENTDB_RUN_WITH|orient|' bin/orientdb.sh
 
  install -m755 bin/console.sh "${pkgdir}"/opt/orientdb/bin/
  install -m755 lib/* "${pkgdir}"/opt/orientdb/lib/
  install -m700 config/* "${pkgdir}"/opt/orientdb/config/
  install -m700 bin/shutdown.sh bin/server.sh bin/orientdb.sh "${pkgdir}"/opt/orientdb/bin/
  cp -r www/* "${pkgdir}"/opt/orientdb/www/
 
  install -m644 "${srcdir}"/orientdb.service "${pkgdir}"/usr/lib/systemd/system/
}
