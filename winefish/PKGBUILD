# Maintainer:  Michele Damiano Torelli <me_AT_mdtorelli_DOT_it>
# Contributor: Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Claudio Riva <firetux83@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=winefish
pkgver=1.3.3
pkgrel=13
pkgdesc="LaTeX editor based on Bluefish with auto-completion and syntax highlighting"
url="http://viettug.github.com/winefish"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'desktop-file-utils')
optdepends=('aspell: for spell check')
install=$pkgname.install
source=("$pkgname-$pkgver.zip"::"http://github.com/viettug/$pkgname/archive/$pkgver.zip"
        'doc_html.patch')
md5sums=('15c9aefb0a6e446a3887e5b05901a962'
         'c908f61d9a3f3f5b9011e238eef6ef68')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p0 < "${srcdir}"/doc_html.patch
}

build() {
  cd ${pkgname}-${pkgver}
  chmod +x configure
  ./configure --prefix=/usr --disable-update-databases \
    --with-freedesktop_org-menu=/usr/share/applications \
    --with-icon-path=/usr/share/pixmaps --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
